/*
 * This file is part of the GROMACS molecular simulation package.
 *
 * Copyright (c) 2012, by the GROMACS development team, led by
 * Mark Abraham, David van der Spoel, Berk Hess, and Erik Lindahl,
 * and including many others, as listed in the AUTHORS file in the
 * top-level source directory and at http://www.gromacs.org.
 *
 * GROMACS is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public License
 * as published by the Free Software Foundation; either version 2.1
 * of the License, or (at your option) any later version.
 *
 * GROMACS is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with GROMACS; if not, see
 * http://www.gnu.org/licenses, or write to the Free Software Foundation,
 * Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301  USA.
 *
 * If you want to redistribute modifications to GROMACS, please
 * consider that scientific software is very special. Version
 * control is crucial - bugs must be traceable. We will be happy to
 * consider code for inclusion in the official distribution, but
 * derived work must not be called official GROMACS. Details are found
 * in the README & COPYING files - if they are missing, get the
 * official version at http://www.gromacs.org.
 *
 * To help us fund GROMACS development, we humbly ask that you cite
 * the research papers on the package. Check out http://www.gromacs.org.
 */

#ifndef VECTYPE_OPS_CLH
#define VECTYPE_OPS_CLH

#define _VECTYPE_OPS_INLINE_

#if defined(_VECTYPE_OPS_INLINE_)
#define _INLINE_ inline
#else
#define _INLINE_ 
#endif

/**** float3 ****/
_INLINE_   float3 make_float3(float4 a)
{
    return (float3)(a.x, a.y, a.z);
}

_INLINE_  __OVERLOADABLE__ float norm(float3 a)
{
    return sqrt(dot(a,a));
}
_INLINE_  __OVERLOADABLE__ float norm_ref(float3 a)
{
    return sqrt(a.x * a.x + a.y * a.y + a.z * a.z);
}
_INLINE_  __OVERLOADABLE__ float norm2(float3 a)
{
    return dot(a,a);
}
_INLINE_  __OVERLOADABLE__ float norm2_ref(float3 a)
{
    return (a.x * a.x + a.y * a.y + a.z * a.z);
}
_INLINE_  __OVERLOADABLE__ float dist3(float3 a, float3 b)
{
    return distance(b,a);
}
_INLINE_  __OVERLOADABLE__ float dist3_ref(float3 a, float3 b)
{
    return norm_ref(b - a);
}
_INLINE_ __OVERLOADABLE__ void atomicAdd(volatile __local float *addr, float val)
{
    union{
        unsigned int u32;
        float        f32;
    } next, expected;
    do{
        expected.f32    = *addr;
        next.f32        = expected.f32 + val;
    } while( atomic_cmpxchg( (volatile __local unsigned int *)addr, expected.u32, next.u32) != expected.u32 );
}
_INLINE_ __OVERLOADABLE__ void atomicAdd(__local float3 *addr, float3 val)
{
    atomicAdd( ((__local float*)(addr)), val.x);
    atomicAdd( ((__local float*)(addr))+1, val.y);
    atomicAdd( ((__local float*)(addr))+2, val.z);
}
_INLINE_ __OVERLOADABLE__ void atomicAdd(volatile __global float *addr, float val)
{
    union{
        unsigned int u32;
        float        f32;
    } next, expected;
    do{
        expected.f32    = *addr;
        next.f32        = expected.f32 + val;
    } while( atomic_cmpxchg( (volatile __global unsigned int *)addr, expected.u32, next.u32) != expected.u32 );
}
_INLINE_ __OVERLOADABLE__ void atomicAdd(__global float3 *addr, float3 val)
{
    atomicAdd( ((__global float*)(addr)), val.x);
    atomicAdd( ((__global float*)(addr))+1, val.y);
    atomicAdd( ((__global float*)(addr))+2, val.z);
}
_INLINE_ __OVERLOADABLE__ void atomicAdd(volatile float *addr, float val)
{
    union{
        unsigned int u32;
        float        f32;
    } next, expected;
    do{
        expected.f32    = *addr;
        next.f32        = expected.f32 + val;
    } while( atomic_cmpxchg( (volatile __local unsigned int *)addr, expected.u32, next.u32) != expected.u32 );
}
_INLINE_ __OVERLOADABLE__ void atomicAdd( float3 *addr, float3 val)
{
    atomicAdd( (( float*)(addr)), val.x);
    atomicAdd( (( float*)(addr))+1, val.y);
    atomicAdd( (( float*)(addr))+2, val.z);
}
/****************************************************************/

/**** float4 ****/
_INLINE_   float4 make_float4(float3 a)
{
    return (float4)(a.x, a.y, a.z, 0.0f);
}

_INLINE_  __OVERLOADABLE__ float norm(float4 a)
{
    return sqrt(dot(a,a));
}

_INLINE_  __OVERLOADABLE__ float norm_ref(float4 a)
{
    return sqrt(a.x * a.x + a.y * a.y + a.z * a.z + a.w * a.w);
}

_INLINE_  __OVERLOADABLE__ float dist3(float4 a, float4 b)
{
    return norm(b - a);
}

_INLINE_  __OVERLOADABLE__ float dist3_ref(float4 a, float4 b)
{
    return norm_ref(b - a);
}
#endif /* VECTYPE_OPS_CLH */
